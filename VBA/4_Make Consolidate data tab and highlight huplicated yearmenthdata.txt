## make Consolidate data tab and highlight huplicated yearmenthdata

Sub CombinedtofirstTab()
    
    
    
    Call UnfilterAllTabs
    Call CreateAndCopyToNewTab
    CombineABCtoX
    HighlightDuplicatesInColumnX
    SelectAllAndAddFilter
    
    
    
End Sub

Sub UnfilterAllTabs()
    Dim ws As Worksheet

    ' Loop through each worksheet in the active workbook
    For Each ws In ActiveWorkbook.Worksheets
        ' Check if there is a filter applied and remove it
        If ws.AutoFilterMode Then
            ws.AutoFilterMode = False
        End If
    Next ws
    
End Sub

Sub CreateAndCopyToNewTab()
    Dim ws As Worksheet
    Dim newSheet As Worksheet
    Dim lastRow As Long
    Dim currentRow As Long
    
    ' Create a new worksheet at the front of the active workbook
    Set newSheet = ActiveWorkbook.Worksheets.Add(Before:=ActiveWorkbook.Worksheets(1))
    newSheet.Name = "ConsolidatedData"  ' Rename as needed
    
    currentRow = 1 ' Start pasting data from the first row
    
    ' Loop through each sheet in the active workbook starting from the second sheet
    For Each ws In ActiveWorkbook.Worksheets
        If ws.Index > 1 Then
            ' Find the last row with data on the current sheet
            lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
            
            ' Copy data from current sheet and paste into the new sheet
            ws.Range("A1:A" & lastRow).EntireRow.Copy
            newSheet.Cells(currentRow, 1).PasteSpecial xlPasteValues
            
            ' Clear clipboard to prevent memory overflow error
            Application.CutCopyMode = False
            
            ' Move to the next row in the new sheet
            currentRow = newSheet.Cells(newSheet.Rows.Count, 1).End(xlUp).Row + 1
        End If
    Next ws

End Sub

Sub CombineABCtoX()
    Dim ws As Worksheet
    Dim lastRow As Long
    Dim i As Long
    
    ' Set worksheet
        Set ws = ActiveWorkbook.Sheets("ConsolidatedData")
    
    ' Find the last row in column A
    lastRow = ws.Cells(ws.Rows.Count, "A").End(xlUp).Row
    
    ' Loop through each row from 2 to last row in column A
    For i = 2 To lastRow
        ws.Cells(i, "X").Formula = "=" & "A" & i & "&B" & i & "&C" & i
    Next i
End Sub


Sub HighlightDuplicatesInColumnX()
    Dim ws As Worksheet
    Dim lastRow As Long
    Dim cell As Range
    Dim rng As Range

    ' Set the worksheet you want to work on
        Set ws = ActiveWorkbook.Sheets("ConsolidatedData")

    ' Find the last row in Column X
    lastRow = ws.Cells(ws.Rows.Count, "X").End(xlUp).Row

    ' Set the range in Column X
    Set rng = ws.Range("X1:X" & lastRow)

    ' Clear previous formatting
    rng.Interior.ColorIndex = xlNone

    ' Loop through each cell in the range
    For Each cell In rng
        If WorksheetFunction.CountIf(rng, cell.Value) > 1 Then
            cell.Interior.Color = vbYellow ' Highlight duplicate cells in yellow
        End If
    Next cell
End Sub

Sub SelectAllAndAddFilter()
    Dim ws As Worksheet
    Set ws = ActiveWorkbook.Sheets("ConsolidatedData")
    
    ' Select all cells in the "ConsolidatedData" sheet
    ws.Cells.Select
    
    ' Auto-fit columns
    ws.Columns.AutoFit
    
    ' Center the content horizontally and vertically in all cells
    With Selection
        .HorizontalAlignment = xlCenter
        .VerticalAlignment = xlCenter
    End With
    
    ' Add a filter to the first row
    ws.Rows("1:1").AutoFilter
End Sub
